expr    		: KEYWORD:CHOL IDENTIFIER EQ expr
						: comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr		: NOY comp-expr
						: arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*
				
arith-expr	: term ((PLUS|MINUS) term)*

term    		: factor ((MUL|DIV) factor)*

factor  		: (PLUS|MINUS) factor
						:	power

power				:	call (POW factor)*

call				:	atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom				:	INT|FLOAT|IDENTIFIER
						: LPAREN expr RPAREN
						: if-expr
						: for-expr
						: while-expr
						: func-def

if-expr			:	KEYWORD:JODI expr KEYWORD:TARPOR expr
							(KEYWORD:nahole-jodi expr KEYWORD:TARPOR expr)*
							(KEYWORD:NAHOLE expr)?

for-expr		: KEYWORD:GHORAO IDENTIFIER EQ expr KEYWORD:THEKE expr 
							(KEYWORD:baro expr)? KEYWORD:TARPOR expr

while-expr	: KEYWORD:JOTOKHON expr KEYWORD:TARPOR expr

func-def		:	KEYWORD:KAAJ IDENTIFIER?
							LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
							ARROW expr